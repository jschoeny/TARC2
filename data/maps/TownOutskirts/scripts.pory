const GUARD1 = 1
const VAR_TEMP_PLAYER_DIR = VAR_TEMP_1

mapscripts TownOutskirts_MapScripts {
  MAP_SCRIPT_ON_LOAD {
    setescapewarp(MAP_TOWN_OUTSKIRTS, 14, 4)
  }
  MAP_SCRIPT_ON_FRAME_TABLE [
    VAR_STORY_PROGRESS, STORY_DEFEATED_GUARDIANS {
      applymovement(LOCALID_PLAYER, moves(face_up))
      waitmovement(LOCALID_PLAYER)
      speakername("GUARD")
      msgbox(format(
        "You're back! I heard you did great out there.\p"
        "So what did you find?"
      ))
      closemessage
      delay(16)
      speakername("GRANDMA")
      msgbox(format(
        "RENJI told the GUARD about the shrine."
      ))
      closemessage
      delay(16)
      speakername("GUARD")
      msgbox(format(
        "“To the top of the mount\Nmust the hero embark…”\p"
        "…"
      ))
      closemessage
      delay(30)
      playse(SE_PIN)
      applymovement(GUARD1, moves(emote_exclamation_mark))
      waitmovement(GUARD1)
      speakername("GUARD")
      msgbox(format(
        "That must be you!\p"
        "You and your fire creature need to\Nclimb to the top of the mountain peak.\p"
        "That's where you will find the\Nlegendary rainbow bird, HO-OH.\p"
        "Then HO-OH can cleanse the land of that blight!\p"
        "…\p"
        "The journey will be dangerous.\p"
        "You should go prepare first.\NI'll be waiting here to prepare\Nfor your expedition."
      ))
      closemessage
      setvar(VAR_STORY_PROGRESS, STORY_RETURNED_AFTER_GUARDIANS)
    }
  ]
}

script TownOutskirts_EventScript_Guard0 {
  msgbox(format(
    "…\p"
    "He appears to be sleeping…"
  ))
  closemessage
  releaseall
}

script TownOutskirts_EventScript_Guard2 {
  speakername("GUARD")
  msgbox(format(
    "Sorry I can't let you through here."
  ), MSGBOX_NPC)
}

script TownOutskirts_Trigger_TooDangerous {
  lockall
  applymovement(GUARD1, moves(
    face_left,
    emote_exclamation_mark,
    delay_16,
    walk_in_place_left,
    delay_16,
  ))
  delay(16)
  applymovement(LOCALID_PLAYER, moves(
    face_right,
  ))
  delay(8)
  applymovement(LOCALID_FOLLOWING_POKEMON, moves(
    face_right,
  ))
  waitmovement(GUARD1)
  waitmovement(LOCALID_PLAYER)
  waitmovement(LOCALID_FOLLOWING_POKEMON)
  speakername("GUARD")
  msgbox(format(
    "It's too dangerous.\n"
    "Magical creatures are on a rampage."
  ))
  closemessage
  applymovement(LOCALID_PLAYER, moves(
    face_down,
    delay_16*4,
    face_right,
  ))
  applymovement(LOCALID_FOLLOWING_POKEMON, moves(
    face_up,
    delay_16*4,
    face_right,
  ))
  waitmovement(LOCALID_FOLLOWING_POKEMON)
  waitmovement(LOCALID_PLAYER)
  delay(30)
  speakername("GUARD")
  msgbox(format(
    "I see…\p"
    "In that case…"
  ))
  closemessage
  delay(16)
  applymovement(GUARD1, moves(
    face_right,
    delay_16*4,
    face_left,
    delay_16*2,
  ))
  waitmovement(GUARD1)
  speakername("GUARD")
  msgbox(format(
    "Look out for something blue and angry.\p"
    "If you get hurt, don't tell anyone.\n"
    "Just come back here."
  ))
  closemessage
  delay(16)
  applymovement(LOCALID_FOLLOWING_POKEMON, moves(
    face_up,
  ))
  waitmovement(LOCALID_FOLLOWING_POKEMON)
  playmoncry(SPECIES_GROWLITHE, CRY_MODE_ENCOUNTER)
  speakername("GROWLITHE")
  msgbox(format(
    "{CREATE_MUGSHOT MUGSHOT_GROWLITHE EMOTE_JOYOUS}"
    "Growl!"
  ))
  closemessage
  waitmoncry
  delay(16)
  speakername("GUARD")
  playse(SE_PIN)
  applymovement(GUARD1, moves(emote_exclamation_mark))
  msgbox(format(
    "Oh! And before you go...\p"
    "You should check if your fire creature can learn any other moves.\p"
    "Just go to its SUMMARY screen and press SELECT.\p"
    "Don't ask me how I know that…\p"
    "But it's not safe to do your training out in the wild.\p"
    "You'll need to come back here for that."
  ))
  closemessage
  applymovement(GUARD1, moves(face_down))
  waitmovement(GUARD1)
  setvar(VAR_STORY_PROGRESS, STORY_INTRO_PASS_GUARD)
  releaseall
}

script TownOutskirts_EventScript_Guard1 {
  speakername("GUARD")
  if (var(VAR_STORY_PROGRESS) >= STORY_ENTERED_MOUNTAIN_SUMMIT) {
    msgbox(format(
      "That was a close call.\p"
      "Are you ready to go back to\Nthe SUMMIT?"
    ), MSGBOX_NPC)
    goto(TownOutskirts_EventScript_GuardMultiChoice)
  } elif (var(VAR_STORY_PROGRESS) >= STORY_RETURNED_AFTER_GUARDIANS) {
    speakername("GUARD")
    msgbox(format(
      "Are you ready to embark to\NMOUNTAIN SUMMIT?"
    ), MSGBOX_NPC)
    goto(TownOutskirts_EventScript_GuardMultiChoice)
  }
  elif (var(VAR_STORY_PROGRESS) >= STORY_ENTERED_ROCKY_PATH) {
    msgbox(format(
      "Are you ready to return to\NROCKY PATH?"
    ), MSGBOX_NPC)
    goto(TownOutskirts_EventScript_GuardMultiChoice)
  } elif (var(VAR_STORY_PROGRESS) >= STORY_RETURNED_AFTER_WOOPER) {
    lock
    faceplayer
    msgbox(format(
      "Are you ready to go to\NROCKY PATH?"
    ), MSGBOX_NPC)
    goto(TownOutskirts_EventScript_GuardMultiChoice)
  } elif (var(VAR_STORY_PROGRESS) == STORY_INTRO_WOOPER_DEFEATED) {
    setvar(VAR_TEMP_PLAYER_DIR, DIR_SOUTH)
    playse(SE_PIN)
    applymovement(GUARD1, moves(emote_exclamation_mark, face_up))
    waitmovement(GUARD1)
    waitse
    delay(30)
    goto(TownOutskirts_EventScript_DefeatedWooper2)
  } elif (var(VAR_STORY_PROGRESS) >= STORY_ENTERED_MOUNTAIN_PATH) {
    msgbox(format(
      "How can I help?"
    ), MSGBOX_NPC)
    goto(TownOutskirts_EventScript_GuardMultiChoice)
  } elif (var(VAR_STORY_PROGRESS) >= STORY_INTRO_PASS_GUARD) {
    msgbox(format(
      "Good luck out there kid."
    ), MSGBOX_NPC)
  } else {
    msgbox(format(
      "It's too dangerous.\n"
      "Magical creatures are on a rampage."
    ), MSGBOX_NPC)
  }
  closemessage
  applymovement(GUARD1, moves(
    face_down,
  ))
  waitmovement(GUARD1)
  releaseall
}

script TownOutskirts_Trigger_SetHealLocation {
  setrespawn(HEAL_LOCATION_TOWN_OUTSKIRTS)
}

script TownOutskirts_Trigger_DefeatedWooper {
  playse(SE_PIN)
  applymovement(GUARD1, moves(emote_exclamation_mark, face_left))
  applymovement(LOCALID_PLAYER, moves(delay_16, face_right))
  waitmovement(GUARD1)
  waitmovement(LOCALID_PLAYER)
  waitse
  delay(30)
  goto(TownOutskirts_EventScript_DefeatedWooper2)
}

script TownOutskirts_EventScript_DefeatedWooper2 {
  lockall
  speakername("GUARD")
  msgbox(format(
    "You're back! And the blue creature?\p"
    "Interesting…\p"
    "It was infected with a shadowy aura?\N"
    "And that's why it was on a rampage?\N"
    "How peculiar…\p"
    "I think I remember hearing of a legend about a shadowy blight that would infect the land.\p"
    "I saw it written on an ancient shrine built near a rocky path…\p"
    "…\p"
    "I feel bad sending a kid out\Nlike this again…"
  ))
  closemessage
  delay(16)
  if (var(VAR_TEMP_PLAYER_DIR) == DIR_SOUTH) {
    applymovement(GUARD1, moves(
      face_right,
      delay_16*4,
      face_up,
      delay_16*2,
    ))
  } else {
    applymovement(GUARD1, moves(
      face_right,
      delay_16*4,
      face_left,
      delay_16*2,
    ))
  }
  waitmovement(GUARD1)
  speakername("GUARD")
  msgbox(format(
    "Okay here's the plan.\p"
    "I'll have a GUARD escort you to the\Nfoot of the ROCKY PATH.\p"
    "Go prepare, then come talk to me when you're ready."
  ))
  closemessage
  applymovement(GUARD1, moves(face_down))
  waitmovement(GUARD1)
  playmoncry(SPECIES_GROWLITHE, CRY_MODE_ENCOUNTER)
  speakername("GROWLITHE")
  msgbox(format(
    "{CREATE_MUGSHOT MUGSHOT_GROWLITHE EMOTE_JOYOUS}"
    "Growl!"
  ))
  closemessage
  waitmoncry
  setvar(VAR_STORY_PROGRESS, STORY_RETURNED_AFTER_WOOPER)
  releaseall
}

script TownOutskirts_EventScript_GuardMultiChoice {
  dynmultipush("Heal", 0)
  if (var(VAR_STORY_PROGRESS) >= STORY_RETURNED_AFTER_GUARDIANS) {
    dynmultipush("MOUNTAIN SUMMIT", 1)
  }
  if (var(VAR_STORY_PROGRESS) >= STORY_RETURNED_AFTER_WOOPER) {
    dynmultipush("ROCKY PATH", 2)
  }
  dynmultipush("Close", 3)
  dynmultistack(0, 0, FALSE, 4, FALSE, 0, NULL)
  switch (var(VAR_RESULT)) {
    case 0:
      closemessage
      call(Common_EventScript_OutOfCenterPartyHeal)
      goto(TownOutskirts_EventScript_GuardMultiChoice_Heal)
    case 1:
      goto(TownOutskirts_EventScript_GuardMultiChoice_MountainSummit)
    case 2:
      goto(TownOutskirts_EventScript_GuardMultiChoice_RockyPath)
    case 3:
      goto(TownOutskirts_EventScript_GuardMultiChoice_Close)
  }
}

script TownOutskirts_EventScript_GuardMultiChoice_Heal {
  speakername("GUARD")
  msgbox(format(
    "Be careful out there kid."
  ), MSGBOX_NPC)
  applymovement(GUARD1, moves(face_down))
  waitmovement(GUARD1)
  releaseall
}

script TownOutskirts_EventScript_GuardMultiChoice_MountainSummit {
  speakername("GUARD")
  if (var(VAR_STORY_PROGRESS) < STORY_ENTERED_MOUNTAIN_SUMMIT) {
    msgbox(format(
      "Great! To the tallest peak!"
    ), MSGBOX_NPC)
    setvar(VAR_STORY_PROGRESS, STORY_ENTERED_MOUNTAIN_SUMMIT)
  } else {
    msgbox(format(
      "Great! You can do this!"
    ), MSGBOX_NPC)
  }
  setrespawn(HEAL_LOCATION_TOWN_OUTSKIRTS)
  warp(MAP_MOUNTAIN_SUMMIT, 0)
}

script TownOutskirts_EventScript_GuardMultiChoice_RockyPath {
  speakername("GUARD")
  msgbox(format(
    "Great! My friend will escort you."
  ), MSGBOX_NPC)
  if (var(VAR_STORY_PROGRESS) < STORY_ENTERED_ROCKY_PATH) {
    setvar(VAR_STORY_PROGRESS, STORY_ENTERED_ROCKY_PATH)
    setvar(VAR_WILD_SHADOW_CHANCE, 20)
  }
  setrespawn(HEAL_LOCATION_ROCKY_PATH1)
  warp(MAP_ROCKY_PATH1, 0)
}

script TownOutskirts_EventScript_GuardMultiChoice_Close {
  speakername("GUARD")
  if (var(VAR_STORY_PROGRESS) >= STORY_RETURNED_AFTER_WOOPER) {
    msgbox(format(
      "Come back when you're ready."
    ), MSGBOX_NPC)
  } else {
    msgbox(format(
      "I'll be here if you need anything."
    ), MSGBOX_NPC)
  }
  applymovement(GUARD1, moves(face_down))
  waitmovement(GUARD1)
  releaseall
}
