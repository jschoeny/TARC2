mapscripts RockyPath3_MapScripts {
  MAP_SCRIPT_ON_LOAD {
    setescapewarp(MAP_ROCKY_PATH1, 25, 15)
    if (var(VAR_TEMP_1) == 0) {
      setflag(FLAG_TEMP_1) // Hide Shadow Vaporeon
      setvar(VAR_TEMP_1, 1)
    }
  }
}

script RockyPath3_TriggerLeft {
  lockall
  applymovement(LOCALID_PLAYER, moves(walk_up*3))
  applymovement(OBJ_EVENT_ID_FOLLOWER, moves(walk_up*3))
  waitmovement(LOCALID_PLAYER)
  waitmovement(OBJ_EVENT_ID_FOLLOWER)
  applymovement(OBJ_EVENT_ID_FOLLOWER, moves(walk_right, walk_up))
  waitmovement(OBJ_EVENT_ID_FOLLOWER)
  goto(RockyPath3_ShadowEonsCutscene)
}

script RockyPath3_TriggerRight {
  lockall
  applymovement(LOCALID_PLAYER, moves(walk_up*3, walk_left, face_up))
  applymovement(OBJ_EVENT_ID_FOLLOWER, moves(walk_up*4))
  waitmovement(LOCALID_PLAYER)
  waitmovement(OBJ_EVENT_ID_FOLLOWER)
  goto(RockyPath3_ShadowEonsCutscene)
}

script RockyPath3_ShadowEonsCutscene {
  speakername("GRANDMA")
  msgbox(format(
    "RENJI and GROWLITHE observed the shrine.\p"
    "It described the legend surrounding the mythical HO-OH."
  ))
  closemessage
  delay(20)
  msgbox(format(
    "“A shadowy blight shall\Ncorrupt the whole land,\p"
    "“Closing the hearts of all who-”"
  ))
  closemessage

  addobject(LOCALID_SHADOW_VAPOREON)
  playmoncry(SPECIES_FLAREON, CRY_MODE_ENCOUNTER)
  applymovement(OBJ_EVENT_ID_FOLLOWER, moves(
    emote_question_mark,
    face_left,
    delay_16,
    face_right,
    delay_16,
    face_left,
  ))
  waitmovement(OBJ_EVENT_ID_FOLLOWER)
  waitmoncry

  playbgm(MUS_PMDRED_RISING_FEAR, TRUE)
  applymovement(LOCALID_SHADOW_FLAREON, moves(
    walk_right,
    jump_down_right,
    jump_down_right,
    walk_right,
    jump_down_right,
    walk_right,
    jump_down_right,
    jump_right,
  ))
  applymovement(LOCALID_SHADOW_JOLTEON, moves(
    walk_left,
    jump_down_left,
    jump_down_left,
    walk_left,
    jump_down_left,
    walk_left,
    jump_down_left,
    jump_left,
  ))
  waitmovement(LOCALID_SHADOW_FLAREON)
  waitmovement(LOCALID_SHADOW_JOLTEON)
  applymovement(LOCALID_PLAYER, moves(
    lock_facing_direction,
    walk_down,
    unlock_facing_direction,
  ))
  applymovement(OBJ_EVENT_ID_FOLLOWER, moves(
    face_up,
    lock_facing_direction,
    walk_down,
    unlock_facing_direction,
  ))
  applymovement(LOCALID_SHADOW_FLAREON, moves(
    walk_right,
    face_down,
  ))
  applymovement(LOCALID_SHADOW_JOLTEON, moves(
    walk_left,
    face_down,
  ))
  applymovement(LOCALID_SHADOW_VAPOREON, moves(walk_up*4))
  waitmovement(OBJ_EVENT_ID_FOLLOWER)
  waitmovement(LOCALID_PLAYER)
  waitmovement(LOCALID_SHADOW_JOLTEON)
  waitmovement(LOCALID_SHADOW_FLAREON)
  waitmovement(LOCALID_SHADOW_VAPOREON)
  delay(30)

  applymovement(LOCALID_PLAYER, moves(
    lock_facing_direction,
    walk_down,
    unlock_facing_direction,
    face_down,
    delay_16*2,
    lock_facing_direction,
    walk_fast_up,
    unlock_facing_direction,
    delay_16,
    face_up,
    delay_16,
    face_down,
    delay_16,
    face_right,
  ))
  applymovement(OBJ_EVENT_ID_FOLLOWER, moves(
    lock_facing_direction,
    walk_down,
    unlock_facing_direction,
    face_down,
    delay_16*2,
    lock_facing_direction,
    walk_fast_up,
    unlock_facing_direction,
    delay_16,
    face_up,
    delay_16,
    face_down,
    delay_16,
    face_left,
  ))
  waitmovement(LOCALID_PLAYER)
  waitmovement(OBJ_EVENT_ID_FOLLOWER)

  speakername("GROWLITHE")
  msgbox(format(
    "{CREATE_MUGSHOT MUGSHOT_GROWLITHE EMOTE_DETERMINED}"
    "Growl!"
  ))
  closemessage
  delay(60)
  applymovement(OBJ_EVENT_ID_FOLLOWER, moves(
    face_up,
  ))
  applymovement(LOCALID_PLAYER, moves(
    face_up,
  ))
  waitmovement(OBJ_EVENT_ID_FOLLOWER)
  waitmovement(LOCALID_PLAYER)
  delay(60)

  playmoncry(SPECIES_FLAREON, CRY_MODE_ENCOUNTER)
  applymovement(LOCALID_SHADOW_FLAREON, moves(
    shake,
  ))
  waitmoncry
  playmoncry(SPECIES_JOLTEON, CRY_MODE_ENCOUNTER)
  applymovement(LOCALID_SHADOW_JOLTEON, moves(
    shake,
  ))
  waitmoncry

  setflag(FLAG_NO_RUNNING_FROM_BATTLE)
  setwildshadowbattle(SPECIES_JOLTEON, 31, ITEM_NONE, TRUE, SPECIES_FLAREON, 31, ITEM_NONE, TRUE)
  dowildbattle

  if (var(VAR_RESULT) == B_OUTCOME_WON) {
    fadescreenswapbuffers(FADE_TO_BLACK)
    playse(SE_FLEE)
    removeobject(LOCALID_SHADOW_FLAREON)
    removeobject(LOCALID_SHADOW_JOLTEON)
    waitse
    fadescreenswapbuffers(FADE_FROM_BLACK)

    applymovement(LOCALID_SHADOW_VAPOREON, moves(walk_up))
    applymovement(LOCALID_PLAYER, moves(
      face_down,
    ))
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(
      face_down,
    ))
    waitmovement(LOCALID_SHADOW_VAPOREON)
    waitmovement(LOCALID_PLAYER)
    waitmovement(OBJ_EVENT_ID_FOLLOWER)
    delay(20)
    playmoncry(SPECIES_VAPOREON, CRY_MODE_ENCOUNTER)
    applymovement(LOCALID_SHADOW_VAPOREON, moves(
      shake,
    ))
    waitmovement(LOCALID_SHADOW_VAPOREON)
    waitmoncry

    setflag(FLAG_NO_RUNNING_FROM_BATTLE)
    setwildshadowbattle(SPECIES_VAPOREON, 36, ITEM_RAWST_BERRY, TRUE)
    dowildbattle
    if (var(VAR_RESULT) == B_OUTCOME_WON) {
      fadescreenswapbuffers(FADE_TO_BLACK)
      playse(SE_FLEE)
      removeobject(LOCALID_SHADOW_VAPOREON)
      waitse
      fadedefaultbgm
      fadescreenswapbuffers(FADE_FROM_BLACK)

      speakername("GRANDMA")
      msgbox(format("Now that the shrine guardians were gone, RENJI and GROWLITHE could\Nfinally observe the shrine."))
      closemessage
      applymovement(LOCALID_PLAYER, moves(
        walk_up,
      ))
      applymovement(OBJ_EVENT_ID_FOLLOWER, moves(
        walk_up,
      ))
      waitmovement(LOCALID_PLAYER)
      waitmovement(OBJ_EVENT_ID_FOLLOWER)
      delay(20)
      msgbox(RockyPath3_Text_Shrine)
      closemessage

      speakername("GROWLITHE")
      applymovement(OBJ_EVENT_ID_FOLLOWER, moves(emote_question_mark))
      msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_GROWLITHE EMOTE_STUNNED}"
        "Growl?"
      ))
      closemessage
      waitmovement(OBJ_EVENT_ID_FOLLOWER)
      delay(60)

      applymovement(LOCALID_PLAYER, moves(face_right))
      applymovement(OBJ_EVENT_ID_FOLLOWER, moves(face_left))
      waitmovement(LOCALID_PLAYER)
      waitmovement(OBJ_EVENT_ID_FOLLOWER)
      delay(60)
      speakername("GRANDMA")
      msgbox(format(
        "The legend was confusing to\NRENJI and GROWLITHE…\p"
        "They decided to go back to town and\Nask the GUARD if he knew anything."
      ))
      closemessage

      clearflag(FLAG_NO_RUNNING_FROM_BATTLE)
      setvar(VAR_WILD_SHADOW_CHANCE, 60)
      setvar(VAR_STORY_PROGRESS, STORY_DEFEATED_GUARDIANS)
    }
  }
  releaseall
}

script RockyPath3_Sign_Shrine {
  msgbox(RockyPath3_Text_Shrine)
  closemessage
}

text RockyPath3_Text_Shrine {
  format(
    "“A shadowy blight shall\Ncorrupt the whole land,\p"
    "“Closing the hearts\Nof all who draw near.\p"
    "“With vengeance will\NFIRE of the sky rain down,\p"
    "“And with power will\NFIRE of the earth make clear.\p"
    "“The FIRE will grow\Nnot by stone, but by bond.\p"
    "“And by stone will the\Nbond seal the evil within.\p"
    "“To the top of the mount\Nmust the hero embark,\p"
    "“And there will be summoned\Nthe FIRE to win.”"
  )
}
